##
# Configuration for zsh.
#

##
# Place the cursor at the end of the line when a history search is found.
#
function history-search-end {
  integer ocursor=$CURSOR

  if [[ $LASTWIDGET = history-beginning-search-*-end ]]; then
    # Last widget called set $hbs_pos.
    CURSOR=$hbs_pos
  else
    hbs_pos=$CURSOR
  fi

  if zle .${WIDGET%-end}; then
    # Success: Go to end of line.
    zle .end-of-line
  else
    # Failure: Restore position.
    CURSOR=$ocursor
    return 1
  fi
}
zle -N history-beginning-search-backward-end history-search-end
zle -N history-beginning-search-forward-end history-search-end

# Bind the up and down arrows to history search.
bindkey '\e[A' history-beginning-search-backward-end
bindkey '\e[B' history-beginning-search-forward-end

# If oh-my-zsh is used, ensure 'escape dot' sequence works like default
# bash/zsh.
bindkey '\e.' insert-last-word
